;@----------------------------------------------------------------------------
	#include "mmc3.i"
;@----------------------------------------------------------------------------
	.global mapper189init

	.struct mmc3Extra
patch:	.byte 0
lwd:	.byte 0

datar0:	.byte 0
	
;@----------------------------------------------------------------------------
.section .text,"ax"
;@----------------------------------------------------------------------------
;@ This mapper is a modified MMC3.
;@ Everything operates just as it does on the MMC3, only the normal PRG regs
;@ (R:6,R:7) are ignored, and a new PRG Reg is used instead.
;@ Used in:
;@ Thunder Warrior
mapper189init:
;@----------------------------------------------------------------------------
	.word write0, write1, mmc3CounterW, mmc3IrqEnableW
	stmfd sp!, {lr}

	bl mmc3Init

	mov r0, #-1
	bl map89ABCDEF_

	adr r0, writel
	str_ r0, rp2A03MemWrite
	str_ r0, m6502WriteTbl+12

	ldr_ r0, prgcrc
	ldr r1, =0x2A9E
	cmp r0, r1
	movne r1, #0
	strb_ r1, patch

	ldmfd sp!, {pc}

;@----------------------------------------------------------------------------
writel:
	cmp addy, #0x4100
	bcc empty_W

	strb_ r0, datar0

	stmfd sp!, {lr}
	mov r1, addy, lsr#8
	cmp r1, #0x41
	bne 0f
	and r0, r0, #0x30
	mov r0, r0, lsr#4
	bl map89ABCDEF_
	b chPatch

0:
	cmp r1, #0x61
	bne chPatch
	and r0, r0, #0x3
	bl map89ABCDEF_

chPatch:
	ldrb_ r0, patch
	ands r0, r0, r0
	beq chEnd

	cmp addy, #0x4800
	bcc 0f
	cmp addy, #0x5000
	bcs 0f
	ldrb_ r1, datar0
	and r0, r1, #1
	tst r1, #0x10
	orrne r0, r0, #0x2
	bl map89ABCDEF_

	ldrb_ r0, datar0
	tst r0, #0x20
	bl mirror2V_
	b chEnd
0:
	cmp addy, #0x5000
	bcc 0f
	cmp addy, #0x5800
	bcs 0f
	ldrb_ r0, datar0
	strb_ r0, lwd
	b chEnd
0:
	cmp addy, #0x5800
	bcc chEnd
	cmp addy, #0x6000
	bcs chEnd

	adr r2, a5000xordat
	ldrb_ r1, lwd
	ldrb r2, [r2, r1]
	ldrb_ r1, datar0
	eor r2, r1, r2
	and r1, addy, #0x3
	add r1, r1, #0x1800
	ldr r0, =NES_XRAM
	strb r2, [r0, r1]
chEnd:
	ldmfd sp!, {pc}

;@----------------------------------------------------------------------------
setBankPPU:
;@----------------------------------------------------------------------------

	ldrb_ r0, patch
	ands r0, r0, r0
	beq mmc3SetBankPpu

	stmfd sp!, {lr}
	mov r1, #0
	ldrb_ r0, chr01
	bl chr2k1k
	mov r1, #2
	ldrb_ r0, chr23
	bl chr2k1k
	mov r1, #4
	ldrb_ r0, chr4
	bl chr1k
	mov r1, #5
	ldrb_ r0, chr5
	bl chr1k
	mov r1, #6
	ldrb_ r0, chr6
	bl chr1k
	ldmfd sp!, {lr}
	mov r1, #7
	ldrb_ r0, chr7
	b chr1k

;@----------------------------------------------------------------------------
write0:
;@----------------------------------------------------------------------------
	tst addy, #1
	bne w8001

	strb_ r0, reg0
	b setBankPPU

w8001:
	strb_ r0, reg1
	ldrb_ r1, reg0
	and r1, r1, #0x7
	adrl_ r2, chr01
	cmp r1, #0x06
	strccb r0, [r2, r1]

	b setBankPPU
;@----------------------------------------------------------------------------
write1:
;@----------------------------------------------------------------------------
	tst r0, #1
	b mirror2V_
	
;@----------------------------------------------------------------------------
a5000xordat:
.byte 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x49, 0x19, 0x09, 0x59, 0x49, 0x19, 0x09
.byte 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x51, 0x41, 0x11, 0x01, 0x51, 0x41, 0x11, 0x01
.byte 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x49, 0x19, 0x09, 0x59, 0x49, 0x19, 0x09
.byte 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x51, 0x41, 0x11, 0x01, 0x51, 0x41, 0x11, 0x01
.byte 0x00, 0x10, 0x40, 0x50, 0x00, 0x10, 0x40, 0x50, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
.byte 0x08, 0x18, 0x48, 0x58, 0x08, 0x18, 0x48, 0x58, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
.byte 0x00, 0x10, 0x40, 0x50, 0x00, 0x10, 0x40, 0x50, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
.byte 0x08, 0x18, 0x48, 0x58, 0x08, 0x18, 0x48, 0x58, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
.byte 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x58, 0x48, 0x18, 0x08, 0x58, 0x48, 0x18, 0x08
.byte 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x50, 0x40, 0x10, 0x00, 0x50, 0x40, 0x10, 0x00
.byte 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x58, 0x48, 0x18, 0x08, 0x58, 0x48, 0x18, 0x08
.byte 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x59, 0x50, 0x40, 0x10, 0x00, 0x50, 0x40, 0x10, 0x00
.byte 0x01, 0x11, 0x41, 0x51, 0x01, 0x11, 0x41, 0x51, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
.byte 0x09, 0x19, 0x49, 0x59, 0x09, 0x19, 0x49, 0x59, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
.byte 0x01, 0x11, 0x41, 0x51, 0x01, 0x11, 0x41, 0x51, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
.byte 0x09, 0x19, 0x49, 0x59, 0x09, 0x19, 0x49, 0x59, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
